// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Or(a=reset, b=load, out=out1);
    Or(a=out1, b=inc, out=loadR);
    Inc16(in=outR, out=inInc);

    Mux16(a=outR, b=inInc, sel=inc, out=in1);
    Mux16(a=in1, b=in, sel=load, out=in2);
    Mux16(a=in2, b[0..15]=false, sel=reset, out=inR);

    Register(in=inR, load=loadR, out=out, out=outR);
}
